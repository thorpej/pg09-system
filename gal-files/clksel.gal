GAL22V10
CLKSEL

NC CLKIN16 CLKIN8 CLKIN4 /CLKSEL16 /CLKSEL8 /CLKSEL4 /CS /RD /RESET  NC     GND
NC D7      D6     D5      D4        D3       D2       D1  D0  RESETH CLKOUT VCC

;
; This is a clock selector.  It takes the 16MHz, 8MHZ, and 4MHz clock
; signals from the first half of the clock generator, and based on an
; external jumper setting, routes the selected clock to the CLKOUT pin.
; In addition, it makes the selected 6809 clock speed (4MHz, 2MHz, or
; 1MHz) readable from an output register (value will be 1, 2, or 4).
;
; Invalid jumper settings result in no clock signal.
;
; Function table for CLKOUT:
;
; CLKIN16 CLKIN8 CLKIN4 CLKSEL16 CLKSEL8 CLKSEL4     CLKOUT
;       x      x      x        F       F       F          L
;       x      x      x        x       T       T          L
;       x      x      x        T       x       T          L
;       x      x      x        T       T       x          L
;       x      x      L        F       F       T          L
;       x      x      H        F       F       T          H
;       x      L      x        F       T       F          L
;       x      H      x        F       T       F          H
;       L      x      x        T       F       F          L
;       H      x      x        T       F       F          H
;
CLKOUT = CLKIN4  * /CLKSEL16 * /CLKSEL8 *  CLKSEL4
       + CLKIN8  * /CLKSEL16 *  CLKSEL8 * /CLKSEL4
       + CLKIN16 *  CLKSEL16 * /CLKSEL8 * /CLKSEL4

;
; Provide an inverted copy of the /RESET signal (RESETH) for devices
; that want an active-high reset signal.
;
RESETH = RESET

;
; Function table for clock speed register:
;
; CLKSEL16 CLKSEL8 CLKSEL4     D7 D6 D5 D4 D3 D2 D1 D0
;        F       F       F      L  L  L  L  L  L  L  L
;        x       T       T      L  L  L  L  L  L  L  L
;        T       x       T      L  L  L  L  L  L  L  L
;        T       T       x      L  L  L  L  L  L  L  L
;        F       F       T      L  L  L  L  L  L  L  H
;        F       T       F      L  L  L  L  L  L  H  L
;        T       F       F      L  L  L  L  L  H  L  L
;
D7.T = GND
D6.T = GND
D5.T = GND
D4.T = GND
D3.T = GND
D2.T =  CLKSEL16 * /CLKSEL8 * /CLKSEL4
D1.T = /CLKSEL16 *  CLKSEL8 * /CLKSEL4
D0.T = /CLKSEL16 * /CLKSEL8 *  CLKSEL4

D7.E = CS * RD
D6.E = CS * RD
D5.E = CS * RD
D4.E = CS * RD
D3.E = CS * RD
D2.E = CS * RD
D1.E = CS * RD
D0.E = CS * RD
