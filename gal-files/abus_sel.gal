GAL22V10
ABUSSEL

BA  BS       RWB      A1      A2      A3   NC NC NC NC  IVV   GND
NC /HALTACK /SYNCACK /INTACK /NORMAL /aCPU NC NC NC NC /aINTC VCC

;
; 6809 states encoded bus Bus Available and Bus Status:
;
; BA     BS     State
; -------------------
;  0      0     Normal (running)
;  0      1     Interrupt or Reset Acknowledge (vector fetch)
;  1      0     Sync Acknowledge
;  1      1     Halt Acknowledge
;
; We don't really need these signals externally yet, but we have
; spare outputs at the moment.
;
NORMAL  = /BA * /BS
INTACK  = /BA *  BS
SYNCACK =  BA * /BS
HALTACK =  BA *  BS

;
; 6809 vector locations.  We can decode A1-A3 to figure out which one
; is being fetched.
;
; $FFFE - $FFFF     Reset	[7]
; $FFFC - $FFFD     NMI		[6]
; $FFFA - $FFFB     SWI		[5]
; $FFF8 - $FFF9     IRQ		[4]
; $FFF6 - $FFF7     FIRQ	[3]
; $FFF4 - $FFF5     SWI2	[2]
; $FFF2 - $FFF3     SWI3	[1]
; $FFF0 - $FFF1     Reserved	[0]
;
; We want the INTC to control the address bus when the IRQ vector is
; being fetched.  N.B. we also need "INTC is in charge" and the vector
; it provides to be stable for the entire vector fetch, which is 2
; cycles, so it will need to latch its state on the rising edge of BS.
;
; We want the CPU to control the address bus in all other circumstances.
;
aCPU  = /BA * /BS					; NORMAL
      + /BA *  BS * /A3 * /A2 * /A1			; Reserved vector
      + /BA *  BS * /A3 * /A2 *  A1			; SWI3
      + /BA *  BS * /A3 *  A2 * /A1			; SWI2
      + /BA *  BS * /A3 *  A2 *  A1			; FIRQ
      + /BA *  BS *  A3 * /A2 * /A1 * /RWB		; IRQ (write)
      + /BA *  BS *  A3 * /A2 * /A1 *  RWB * /IVV	; IRQ (read, not valid)
      + /BA *  BS *  A3 * /A2 *  A1			; SWI
      + /BA *  BS *  A3 *  A2 * /A1			; NMI
      + /BA *  BS *  A3 *  A2 *  A1			; RESET
      +  BA * /BS					; SYNCACK
      +  BA *  BS					; HALTACK

aINTC = /BA *  BS *  A3 * /A2 * /A1 *  RWB *  IVV	; IRQ (read, valid)

DESCRIPTION

This selects the source of the address bus for a given bus transaction
based on a variety of selection criteria.
